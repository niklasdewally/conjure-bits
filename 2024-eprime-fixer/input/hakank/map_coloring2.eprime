$
$ Simple coloring problem in Essence'.
$
$ From Pascal Van Hentenryck "The OPL Optimization Programming Language", 
$ page 7, 42.
$
$ This is a more general solution than 
$ http://www.hakank.org/savile_row/map_coloring.eprime .
$
$ Model created by Hakan Kjellerstrand, hakank@gmail.com
$ See also my Essence'/Tailor page: http://www.hakank.org/savile_row/
$
language ESSENCE' 1.0

letting n be 6 $ number of countries
letting numColors = 4

letting num_countries be 6
letting countries be domain int(1..num_countries)
$  [belgium, denmark, france, germany, netherlands, luxembourg]
letting connections = 
  [[0, 0, 1, 1, 1, 1],
   [0, 0, 0, 1, 0, 0],
   [1, 0, 0, 1, 1, 0],
   [1, 1, 1, 0, 1, 1],
   [1, 0, 1, 1, 0, 0],
   [1, 0, 0, 1, 0, 0]]

find color : matrix indexed by [int(1..n)] of int(1..numColors)

such that
  forAll i,j : countries . 
     (i < j /\ connections[i,j] = 1) -> color[i] != color[j],
 
  $ symmetry breaking: belgium has color 1
  color[1] = 1



