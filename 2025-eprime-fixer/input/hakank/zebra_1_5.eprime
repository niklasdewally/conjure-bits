$
$ Zebra puzzle in Essence'.
$
$ From zebra.mzn in the MiniZinc distribution.
$ """
$ WHO OWNS THE ZEBRA?
$ 
$ This is a puzzle which has been circulating the net. There are a couple
$ different versions out there which try to be a little more politically
$ correct but are essentially the same problem.    
$ 	1. There are five houses, each of a different color and inhabited by
$	   men of different nationalities, with different pets, drinks,
$	   and cigarettes.
$ 	2. The Englishman lives in the red house.
$ 	3. The Spaniard owns the dog.
$ 	4. Coffee is drunk in the green house.
$ 	5. The Ukrainian drinks tea.
$ 	6. The green house is immediately to the right of the ivory house.
$ 	7. The Old Gold smoker owns snails.
$ 	8. Kools are smoked in the yellow house.
$ 	9. Milk is drunk in the middle house.
$ 	10. The Norwegian lives in the first house on the left.
$ 	11. The man who smokes Chesterfields lives in the house next to the
$	    man with the fox.
$ 	12. Kools are smoked in the house next to the house where the horse is
$	    kept.
$ 	13. The Lucky Strike smoker drinks orange juice.
$ 	14. The Japanese smoke Parliaments.
$ 	15. The Norwegian lives next to the blue house.
$  NOW, who drinks water? And who owns the zebra?
$ """
$
$ Also, see http://en.wikipedia.org/wiki/Zebra_Puzzle
$

$
$ Model created by Hakan Kjellerstrand, hakank@gmail.com
$ See also my Essence'/Tailor page: http://www.hakank.org/savile_row/
$
language ESSENCE' 1.0

letting English be 1
letting Spanish be 2
letting Ukrainian be 3
letting Norwegian be 4
letting Japanese be 5

letting Red be 1
letting Green be 2
letting Ivory be 3
letting Yellow be 4
letting Blue be 5

letting Dog be 1
letting Fox be 2
letting Horse be 3
letting Zebra be 4
letting Snails be 5

letting Coffee be 1
letting Tea be 2
letting Milk be 3
letting OrangeJuice be 4
letting Water be 5

letting OldGold be 1
letting Kools be 2
letting Chesterfields be 3
letting LuckyStrike be 4
letting Parliaments be 5

find animal, colour, drink, nation, smoke : matrix indexed by [int(1..5)] of int(1..5)
find owns_zebra: int(1..5)
find drinks_water: int(1..5)


such that
  allDiff(animal),                          $ 1
  allDiff(colour),
  allDiff(drink),
  allDiff(nation),
  allDiff(smoke),

  nation[English] = colour[Red],            $ 2 
  nation[Spanish] = animal[Dog],            $ 3
  drink[Coffee] = colour[Green],            $ 4
  nation[Ukrainian] = drink[Tea],           $ 5
  colour[Green] = colour[Ivory] + 1,        $ 6
  smoke[OldGold] = animal[Snails],          $ 7
  smoke[Kools] = colour[Yellow],            $ 8
  drink[Milk] = 3,                          $ 9
  nation[Norwegian] = 1,                    $ 10
  |smoke[Chesterfields] - animal[Fox]| = 1, $ 11
  |smoke[Kools] - animal[Horse]| = 1,       $ 12
  smoke[LuckyStrike] = drink[OrangeJuice],  $ 13
  nation[Japanese] = smoke[Parliaments],    $ 14
  |nation[Norwegian] - colour[Blue]| = 1,   $ 15

  $ who owns the zebra?
  owns_zebra = animal[Zebra],
  $ who drinks the water?
  drinks_water = drink[Water]
