$
$ Photo problem  in Essence'.
$ 
$ Problem statement from Mozart/Oz tutorial:
$ http://www.mozart-oz.org/home/doc/fdt/node37.html#section.reified.photo
$ """
$ Betty, Chris, Donald, Fred, Gary, Mary, and Paul want to align in one 
$ row for taking a photo. Some of them have preferences next to whom they want to stand:
$
$    1. Betty wants to stand next to Gary and Mary.
$    2. Chris wants to stand next to Betty and Gary.
$    3. Fred wants to stand next to Mary and Donald.
$    4. Paul wants to stand next to Fred and Donald.
$
$ Obviously, it is impossible to satisfy all preferences. Can you find an 
$ alignment that maximizes the number of satisfied preferences?
$ """
$ Oz' solution: 
$   6 # alignment(betty:5  chris:6  donald:1  fred:3  gary:7   mary:4   paul:2)
$ [5, 6, 1, 3, 7, 4, 2]
$ 
$
$ Model created by Hakan Kjellerstrand, hakank@gmail.com
$ See also my Essence'/Savile Row page: http://www.hakank.org/savile_row/
$
language ESSENCE' 1.0

letting n be 7
letting range be domain int(1..n)
letting preferences = 
[
  [0,0,0,0,1,1,0], $ Betty  1
  [1,0,0,0,1,0,0], $ Chris  2
  [0,0,0,0,0,0,0], $ Donald 3
  [0,0,1,0,0,1,0], $ Fred   4
  [0,0,0,0,0,0,0], $ Gary   5
  [0,0,0,0,0,0,0], $ Mary   6
  [0,0,1,1,0,0,0]  $ Paul   7
]

find positions: matrix indexed by [range] of range
find z: int(0..n*n)

maximising z

such that
   allDiff(positions),
   z = sum([ |positions[i]-positions[j]| = 1 | i,j: range, preferences[i,j] > 0])

