$
$ Place number puzzle in Essence'.
$
$ http://ai.uwaterloo.ca/~vanbeek/Courses/Slides/introduction.pdf
$ """
$ Place numbers 1 through 8 on nodes
$ - each number appears exactly once
$ - no connected nodes have consecutive numbers
$      2 - 5 
$    / | X | \
$  1 - 3 - 6 - 8
$    \ | X | /
$      4 - 7
$ """
$
$ Two solutions (plus their reverses):
$ x = [2, 5, 8, 6, 3, 1, 4, 7] 
$ x = [2, 6, 8, 5, 4, 1, 3, 7]
$
$
$ Model created by Hakan Kjellerstrand, hakank@gmail.com
$ See also my Essence'/Tailor page: http://www.hakank.org/savile_row/
$
language ESSENCE' 1.0

letting n be 8
letting m be 32

letting graph = 
[
  [1,2],
  [1,3],
  [1,4],
  [2,1],
  [2,3],
  [2,5],
  [2,6],
  [3,2],
  [3,4],
  [3,6],
  [3,7],
  [4,1],
  [4,3],
  [4,6],
  [4,7],
  [5,2],
  [5,3],
  [5,6],
  [5,8],
  [6,2],
  [6,3],
  [6,4],
  [6,5],
  [6,7],
  [6,8],
  [7,3],
  [7,4],
  [7,6],
  [7,8],
  [8,5],
  [8,6],
  [8,7]
]

find x : matrix indexed by [int(1..n)] of int(1..n)

such that
  allDiff(x),
  forAll i: int(1..m) . |x[graph[i,1]]-x[graph[i,2]]| > 1,
  x[1] < x[n]  $ symmetry breaking
