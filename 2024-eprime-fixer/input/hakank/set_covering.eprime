$
$ Simple set covering problem in Essence'.
$
$ Placing of fire stations. 
$ From Winston "Operations Research", page 486
$
$ Model created by Hakan Kjellerstrand, hakank@gmail.com
$ See also my Essence'/Tailor page: http://www.hakank.org/savile_row/
$
language ESSENCE' 1.0

letting min_distance be 15
letting numCities be 6
letting distance: matrix indexed by [int(1..numCities), int(1..numCities)] of int(0..100) be 
   [[0,10,20,30,30,20],
   [10, 0,25,35,20,10],
   [20,25, 0,15,30,20],
   [30,35,15, 0,15,25],
   [30,20,30,15, 0,14],
   [20,10,20,25,14,0]]
   
find x: matrix indexed by [int(1..numCities)] of int(0..1)
find z: int(0..1000)

minimising z

such that
   forall j: int(1..numCities) .
      (sum i: int(1..numCities) . x[i]*(distance[i,j] <= min_distance) ) >= 1
   ,
   z = sum i: int(1..numCities) . x[i]

   
