$ http://www.survo.fi/puzzles/280708.txt, third puzzle
$ Survo puzzle 128/2008 (1700) #364-35846
$
$    A  B  C  D  E  F
$ 1  *  *  *  *  *  * 30
$ 2  *  * 18  *  *  * 86
$ 3  *  *  *  *  *  * 55
$   22 11 42 32 27 37
language ESSENCE' 1.0
letting r be 3
letting c be 6
letting rowsums be [30, 86, 55]
letting colsums be [22, 11, 42, 32, 27, 37]
letting game be
   [
     [0, 0,  0, 0, 0, 0],
     [0, 0, 18, 0, 0, 0],
     [0, 0,  0, 0, 0, 0]
   ]

$ given r : int(..)
$ given c : int(..)
$ given rowsums : matrix indexed by [int(1..r)] of int(1..100000)
$ given colsums : matrix indexed by [int(1..c)] of int(1..100000)
$ given game : matrix indexed by [int(1..r),int(1..c)] of int(0..(r * c))
letting rows be domain int(1..r)
letting cols be domain int(1..c)
find x : matrix indexed by [rows,cols] of int(1..(r * c))
such that
allDiff(flatten(x)),
(forAll i : rows . (forAll j : cols . ((0<game[i, j]) -> (x[i, j]=game[i, j])))),
(forAll i : rows . (((sum j : cols . x[i, j]))=rowsums[i])),
(forAll j : cols . (((sum i : rows . x[i, j]))=colsums[j]))
