$
$ Bales of hay problem in Essence'.
$
$ From The Math Less Traveled, 
$ "The haybaler", http://www.mathlesstraveled.com/?p=582 
$ """
$ You have five bales of hay.
$
$ For some reason, instead of being weighed individually, they were weighed 
$ in all possible combinations of two. The weights of each of these 
$ combinations were written down and arranged in numerical order, without 
$ keeping track of which weight matched which pair of bales. The weights, 
$ in kilograms, were 80, 82, 83, 84, 85, 86, 87, 88, 90, and 91.
$
$ How much does each bale weigh? Is there a solution? Are there multiple 
$ possible solutions? 
$ """
$
$ Model created by Hakan Kjellerstrand, hakank@gmail.com
$ See also my Essence'/Tailor page: http://www.hakank.org/savile_row/
$
language ESSENCE' 1.0

letting n be 5
letting nrange be domain int(1..n)
letting m be 10
letting mrange be domain int(1..m)

letting weights = [80, 82, 83, 84, 85, 86, 87, 88, 90, 91]

find bales: matrix indexed by [nrange] of int(0..50)

$ Essence' / Savile row don't support temporary variables.
$ Hence we have to define them here...
find b1: matrix indexed by [mrange] of nrange
find b2: matrix indexed by [mrange] of nrange

such that
   forall w: int(1..10) .
       b1[w] < b2[w] /\
       bales[b1[w]] + bales[b2[w]] = weights[w]
   ,
   $ increasing
   forall w: int(1..n-1) .
     bales[w] <= bales[w+1]
