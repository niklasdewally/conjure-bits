$
$ Sicherman dice in Essence'.
$
$ From http://en.wikipedia.org/wiki/Sicherman_dice
$ """ 
$ Sicherman dice are the only pair of 6-sided dice which are not normal dice, 
$ bear only positive integers, and have the same probability distribution for 
$ the sum as normal dice.
$ 
$ The faces on the dice are numbered 1, 2, 2, 3, 3, 4 and 1, 3, 4, 5, 6, 8.
$ """
$
$ I read about this problem in a book/column by Martin Gardner long
$ time ago, and got inspired to model it now by the WolframBlog post
$ "Sicherman Dice": http://blog.wolfram.com/2010/07/13/sicherman-dice/
$
$ This model gets the two different ways, first the standard way and
$ then the Sicherman dice:
$ 
$ x1 = array1d(1..6, [1, 2, 3, 4, 5, 6]);
$ x2 = array1d(1..6, [1, 2, 3, 4, 5, 6]);
$ ----------
$ x1 = array1d(1..6, [1, 2, 2, 3, 3, 4]);
$ x2 = array1d(1..6, [1, 3, 4, 5, 6, 8]);
$
$
$ Extra: If we also allow 0 (zero) as a valid value then the 
$ following two solutions are also valid:
$ 
$ x1 = array1d(1..6, [0, 1, 1, 2, 2, 3]);
$ x2 = array1d(1..6, [2, 4, 5, 6, 7, 9]);
$ ----------
$ x1 = array1d(1..6, [0, 1, 2, 3, 4, 5]);
$ x2 = array1d(1..6, [2, 3, 4, 5, 6, 7]);
$ 
$ These two extra cases are mentioned here:
$ http://mathworld.wolfram.com/SichermanDice.html
$
$
$ Model created by Hakan Kjellerstrand, hakank@gmail.com
$ See also my Essence'/Tailor page: http://www.hakank.org/savile_row/
$
language ESSENCE' 1.0

letting n be 6   $ number of sides of each die
letting m be 10  $ max integer

$ standard distribution
letting standard_dist = [1,2,3,4,5,6,5,4,3,2,1; int(2..12)]

$ the two dice
find x1 : matrix indexed by [int(1..n)] of int(0..m)
find x2 : matrix indexed by [int(1..n)] of int(0..m)

such that
  forAll k: int(2..12) .
       standard_dist[k] = sum([x1[i]+x2[j] = k | i,j: int(1..n)]) 
  ,

  $ symmetry breaking
  forAll i: int(2..n) .
     x1[i-1] <= x1[i] /\
     x2[i-1] <= x2[i]
  ,

  $ x1 is less or equal to x2
  forAll i: int(1..n) . x1[i] <= x2[i]
  $ x1 <lex x2
